apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.greenrobot.greendao'
android {
    def globalConfiguration = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfiguration.android["compileSdkVersion"]
    defaultConfig {
        applicationId "com.lishide.gankarms"
        minSdkVersion globalConfiguration.android["minSdkVersion"]
        targetSdkVersion globalConfiguration.android["targetSdkVersion"]
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner globalConfiguration.dependencies["androidJUnitRunner"]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        // dex突破65535的限制
        multiDexEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86_64", "x86"
        }

        flavorDimensions rootProject.ext.android.defultDimensions
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            buildConfigField "boolean", "IS_BUILD_MODULE", "${isBuildModule}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            buildConfigField "boolean", "IS_BUILD_MODULE", "${isBuildModule}"
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        official {
            manifestPlaceholders = [
                    APP_NAME: "@string/app_name"
            ]
        }

        dev {
            applicationId = "com.lishide.gankarms.dev"
            manifestPlaceholders = [
                    APP_NAME: "@string/app_name_test"
            ]
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }

    greendao {
        schemaVersion 1//数据库版本号
        daoPackage 'com.lishide.gankarms.app.greendao'//设置DaoMaster、DaoSession、Dao包名
        targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def appDependencies = rootProject.ext.dependencies

    implementation project(":CommonSDK")
    //因为 CommonRes 依赖了 CommonSDK, 所以如果业务模块需要公共 UI 组件就依赖 CommonRes, 如果不需要就只依赖 CommonSDK
    implementation project(":CommonRes")
    implementation project(":CommonService")

    //tools
    kapt rootProject.ext.dependencies["arouter-compiler"]
    kapt rootProject.ext.dependencies["dagger2-compiler"]
    //view
    kapt(rootProject.ext.dependencies["butterknife-compiler"]) {
        exclude module: 'support-annotations'
    }
    //test
    testImplementation rootProject.ext.dependencies["junit"]
    debugImplementation rootProject.ext.dependencies["canary-debug"]
    //anko
    implementation appDependencies["anko-commons"]
    implementation appDependencies["okhttp-logging"]
    implementation appDependencies["greendao"]
    implementation appDependencies["bottombar"]
}
